/*
 * Copyright (c) 2023. Compose Cupertino project and open source contributors.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 *
 */
import com.android.build.api.dsl.androidLibrary
import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    org.jetbrains.kotlin.multiplatform
    com.android.kotlin.multiplatform.library
    alias(libs.plugins.composeJB)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.serialization)
}

val _jvmTarget = findProperty("jvmTarget") as String

kotlin {

    applyDefaultHierarchyTemplate()

    androidLibrary {
        namespace = "com.example.shared"
        compileSdk = (findProperty("android.compileSdk") as String).toInt()

        minSdk {
            release((findProperty("android.minSdk") as String).toInt())
        }
        compilerOptions {
            jvmTarget = JvmTarget.fromTarget(_jvmTarget)
        }
    }

    jvm("desktop")
    js(IR) {
        browser()
    }
    @OptIn(ExperimentalWasmDsl::class)
    wasmJs {
        browser()
    }

    listOf(
        iosX64(),
        iosArm64(),
        iosSimulatorArm64()
    ).forEach { iosTarget ->
        iosTarget.binaries.framework {
            baseName = "shared"
            isStatic = true

            export(libs.decompose.core)
            export(libs.essenty)
            export("com.arkivanov.essenty:lifecycle:${libs.versions.essenty}")
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation(project(":cupertino"))
                implementation(project(":cupertino-native"))
                implementation(project(":cupertino-adaptive"))
                implementation(project(":cupertino-decompose"))
                implementation(project(":cupertino-icons-extended"))
                implementation(libs.material.kolor)

                api(libs.decompose.core)
                api(libs.essenty)
                implementation(libs.decompose.compose)
                implementation(libs.compose.runtime)
                implementation(libs.compose.ui)
                implementation(libs.compose.foundation)
                implementation(libs.compose.material)
                implementation(libs.compose.material3)
                implementation(libs.datetime)
                implementation(libs.material.icons.core)
                implementation(libs.serialization)
            }
        }
        androidMain.dependencies {
            implementation(libs.activity.compose)
        }
    }
}
